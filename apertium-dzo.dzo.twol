Alphabet
 A B Ç D E Ä F G H I J Ž K L M N Ň O Ö P R S Ş T U Ü W Y Ý Z
 a b ç d e ä f g h i j ž k l m n ň o ö p r s ş t u ü w y ý z
 %{A%}:a %>:0
 ཀ ཁ ག ང ཅ ཆ ཇ ཉ ཏ ཐ ད ན པ ཕ བ མ ཙ ཚ ཛ ཝ ཞ ཟ འ ཡ ར ལ ཤ ས ཧ ཨ  ི ུ  ེ  ོ ཊ ཋ ཌ ཎ
 ྐ ྑ ྒ ྔ ྕ ྖ ྗ ྙ ྟ ྠ ྡ ྣ ྤ ྥ ྦ ྨ ྩ ྪ ྫ ྭ ྯ ཱ ྱ ྲ ླ ྴ ྶ ྷ ྸ  ྀ ྄  ཻ  ཽ ྚ ྛ ྜ ྞ
 ༉ ༡ ༢ ༣ ༤ ༥ ༦ ༧ ༨ ༩ ༠ ་ །
%{Q%}:ཚ %{Q%}:ཆ
%{N%}:ུ %{N%}:་
%{v%}:0 %{v%}:ཁ
%{d%}:0 %{d%}:ྱ
%{j%}:0 %{j%}:བ

%{G%}:ག %{G%}:ཀ %{G%}:འ %{G%}:ཡ
%{e%}:0 %{e%}:ྱ
%{T%}:ི

%{L%}:ན %{L%}:ཁ
%{O%}:ང %{O%}:ར

%{P%}:ཅ %{P%}:ཡ

%{n%}:0 %{n%}:མ
%{g%}:0 %{g%}:ི
%{w%}:0 %{w%}:་

%{neg%}:0

%{loc%}:0

%{pl%}:0

%{r%}:0 %{r%}:་ ;




Sets

Vowel = A E Ä I O Ö U Ü Y Ý
        a e ä i o ö u ü y ý
        ི ུ  ེ  ོ
        ྀ ྄  ཻ  ཽ ;

PNM = པ ན མ ;
GEN1 = ན མ ར ལ ;
GEN2 = ད བ ས ;
GEN3 = ག ང ;
GEN5 = འ ;

Rules

"Witnessed past is ཅི་ when verb ends in པ་, ན་ or མ་"
%{P%}:ཅ <=> :PNM :་ %>:0 :* _ ;

"Genitive is གྱི་ when verb ends in ན མ ར ལ (1)"
%{G%}:ག <=> :GEN1 :་ %>:0 :* _ ;

"Genitive is གྱི་ when verb ends in ན མ ར ལ (2)"
%{e%}:ྱ <=> :GEN1 :་ %>:0 :* _ ;

"Genitive is ཀྱི་ when verb ends in ད བ ས (1)"
%{G%}:ཀ <=> :GEN2 :་ %>:0 :* _ ;

"Genitive is ཀྱི་ when verb ends in ད བ ས (2)"
%{e%}:ྱ <=> :GEN2 :་ %>:0 :* _ ;

"Genitive is གི་ when verb ends in ག ང (1)"
%{G%}:ག <=> :GEN3 :་ %>:0 :* _ ;

"Genitive is གི་ when verb ends in ག ང (2)"
%{e%}:0 <=> :GEN3 :་ %>:0 :* _ ;

"Genitive is ཡི་ when verb ends in འ (1)"
%{G%}:ཡ <=> :GEN5 :་ %>:0 :* _ ;

"Genitive is ཡི་ when verb ends in འ (2)"
%{e%}:0 <=> :GEN5 :་ %>:0 :* _ ;

"Genitive is གི་ when verb ends in vowel and unmarked (1)"
%{G%}:ག <=> :Vowel :་ %>:0 :* _ ;

"Genitive is གི་ when verb ends in vowel and unmarked (2)"
%{e%}:0 <=> :Vowel :་ %>:0 :* _ ;

"Genitive is འི་ when verb is marked (1)"
%{G%}:འ <=> %{r%}: :* _ ;

"Genitive is འི་ when verb is marked (2)"
%{e%}:0 <=> %{r%}: :* _ ;

"Remove syllable boundary when suffix is འི "
%{r%}:0 <=> _ %>: :འ :0 :ི ;

"Negative is མ only when verb is past tense"
%{g%}:0 <=> _ :* %{neg%}: ;

"Special Locative Suffix with some nominals(1)"
%{L%}:ཁ <=> %{loc%}: :* _ ;

"Special Locative Suffix with some nominals(2)"
%{O%}:ར <=> %{loc%}: :* _ ;

"Special Plural with some nouns(1)"
%{Q%}:ཆ <=> %{pl%}: :* _ ;

"Special Plural with some nouns(2)"
%{N%}:་ <=> %{pl%}: :* _ ;

"Special Plural with some nouns(3)"
%{v%}:ཁ <=> %{pl%}: :* _ ;

"Special Plural with some nouns(4)"
%{d%}:ྱ <=> %{pl%}: :* _ ;

"Special Plural with some nouns(5)"
%{j%}:བ <=> %{pl%}: :* _ ;

! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Enter_twol
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! https://github.com/hfst/
! http://wiki.apertium.org/wiki/Twol
! http://wiki.apertium.org/wiki/Hfst
